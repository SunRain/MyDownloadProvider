apply plugin: 'android'

dependencies {
	//android.jar
	//compile files("${android.sdk}/platforms/${android.target}/android.jar")

	compile fileTree(dir: 'libs', include: '*.jar')

	compile project(':library:DownloadProvider-lib')
}

android {
	packagingOptions {
		exclude 'META-INF/DEPENDENCIES.txt'
		exclude 'META-INF/LICENSE.txt'
		exclude 'META-INF/NOTICE.txt'
	}

	lintOptions { abortOnError false }
}

//=====================拷贝so文件=======================
task copyNativeLibs(type: Copy) {
	from(new File('libs')) { include '**/*.so' }
	into new File(buildDir, 'native-libs')
}

tasks.withType(Compile) { compileTask -> compileTask.dependsOn copyNativeLibs }

clean.dependsOn 'cleanCopyNativeLibs'

tasks.withType(com.android.build.gradle.tasks.PackageApplication) { pkgTask ->
	pkgTask.jniFolders = new HashSet<File>()
	pkgTask.jniFolders.add(new File(buildDir, 'native-libs'))
}
//=======================================================

android {
	compileSdkVersion 19
	buildToolsVersion "19"

	signingConfigs {
		debug { storeFile file("keystore/debug.keystore") }
	}

	buildTypes {
		release {
			signingConfig signingConfigs.debug
			runProguard true
			proguardFile getDefaultProguardFile('proguard-android.txt')
			proguardFile new File(project(':library:WdUtil').getProjectDir(), 'util-proguard-project.txt')
			proguardFile 'proguard-project.txt'
		}

		debug {
			signingConfig signingConfigs.debug
			runProguard false
		}
	}

	//设置不同包名的包
	productFlavors { original { packageName = 'me.android.download.ui' } }

	sourceSets {
		main {
			manifest.srcFile 'AndroidManifest.xml'
			java.srcDirs = ['src']
			resources.srcDirs = ['src']
			aidl.srcDirs = ['src']
			renderscript.srcDirs = ['src']
			res.srcDirs = ['res']
			assets.srcDirs = ['assets']
		}

		// Move the tests to tests/java, tests/res, etc...
		instrumentTest.setRoot('tests')

		// Move the build types to build-types/<type>
		// For instance, build-types/debug/java, build-types/debug/AndroidManifest.xml, ...
		// This moves them out of them default location under src/<type>/... which would
		// conflict with src/ being used by the main source set.
		// Adding new build types or product flavors should be accompanied
		// by a similar customization.
		original.setRoot('build-types/original')
	}
}
